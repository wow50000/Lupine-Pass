#define GUILDMASTER_ANNOUNCEMENT_COOLDOWN (2 MINUTES)

/datum/job/roguetown/guildmaster
	title = TITLE_HAMLET_SMITH
	flag = GUILDMASTER
	department_flag = YEOMEN
	faction = "Station"
	total_positions = 1
	spawn_positions = 1
	min_pq = 0
	selection_color = JCOLOR_YEOMAN

	allowed_races = RACES_ALL_KINDS

	tutorial = "You’re the hamlet’s smith.  Where you might have once made horseshoes and nails, you now smith weapons and armor.  The influx of adventurers has certainly proved profitable."

	outfit = /datum/outfit/job/roguetown/guildmaster
	selection_color = JCOLOR_YEOMAN
	display_order = JDO_GUILDMASTER
	give_bank_account = 25
	min_pq = 5 // Higher PQ requirement as it is a leadership role. Not for total newbie.
	max_pq = null
	round_contrib_points = 3
	cmode_music = 'sound/music/cmode/towner/combat_retired.ogg'

	job_traits = list(TRAIT_TRAINED_SMITH, TRAIT_SEEPRICES)

	advclass_cat_rolls = list(CTAG_GUILDSMASTER = 2)
	job_subclasses = list(
		/datum/advclass/guildmaster
	)

/datum/job/roguetown/guildmaster/after_spawn(mob/living/L, mob/M, latejoin = TRUE)
	..()
	if(ishuman(L))
		var/mob/living/carbon/human/H = L
		H.advsetup = 1
		H.invisibility = INVISIBILITY_MAXIMUM
		H.become_blind("advsetup")

/datum/advclass/guildmaster
	name = TITLE_HAMLET_SMITH
	tutorial = "You are the leader of the Vale's Guild of Crafts. You represents the interests of all of the craftsmen underneath you - including the Tailor\
	the Blacksmiths, the Artificers and the Architects. Other townspeople may look to you for guidance, but they are not under your control. You are an experienced smith and artificer, and can do their work easily. Protect the craftsmen's interests."
	outfit = /datum/outfit/job/roguetown/guildmaster/basic
	category_tags = list(CTAG_GUILDSMASTER)
	subclass_stats = list(
		STATKEY_STR = 2,
		STATKEY_CON = 2,
		STATKEY_END = 2,
		STATKEY_INT = 1
	)

/datum/outfit/job/roguetown/guildmaster/basic/pre_equip(mob/living/carbon/human/H)
	H.adjust_blindness(-3)
	head = /obj/item/clothing/head/roguetown/chaperon/noble/guildmaster
	gloves = /obj/item/clothing/gloves/roguetown/angle/grenzelgloves/blacksmith
	if(H.mind)
		// Skillset is a combo of Artificer + Blacksmith with Labor Skills.
		// And Tailor / Leathercrafting
		H.verbs += /mob/living/carbon/human/proc/guild_announcement
		H.adjust_skillrank(/datum/skill/combat/axes, 3, TRUE)
		H.adjust_skillrank(/datum/skill/combat/maces, 3, TRUE)
		H.adjust_skillrank(/datum/skill/combat/unarmed, 3, TRUE)
		H.adjust_skillrank(/datum/skill/combat/wrestling, 3, TRUE)
		H.adjust_skillrank(/datum/skill/labor/lumberjacking, 3, TRUE)
		H.adjust_skillrank(/datum/skill/labor/mining, 3, TRUE)
		H.adjust_skillrank(/datum/skill/craft/crafting, 3, TRUE)
		H.adjust_skillrank(/datum/skill/craft/carpentry, 3, TRUE)
		H.adjust_skillrank(/datum/skill/craft/masonry, 3, TRUE)
		H.adjust_skillrank(/datum/skill/craft/blacksmithing, 5, TRUE)
		H.adjust_skillrank(/datum/skill/craft/armorsmithing, 5, TRUE)
		H.adjust_skillrank(/datum/skill/craft/weaponsmithing, 5, TRUE)
		H.adjust_skillrank(/datum/skill/craft/smelting, 4, TRUE)
		H.adjust_skillrank(/datum/skill/craft/engineering, 5, TRUE)
		H.adjust_skillrank(/datum/skill/misc/sewing, 3, TRUE) // Worse than the real tailor, so can't steal their job right away
		H.adjust_skillrank(/datum/skill/craft/tanning, 3, TRUE)
		H.adjust_skillrank(/datum/skill/misc/lockpicking, 4, TRUE)
		H.adjust_skillrank(/datum/skill/misc/ceramics, 2, TRUE)
		H.adjust_skillrank(/datum/skill/craft/traps, 2, TRUE)
		H.adjust_skillrank(/datum/skill/misc/reading, 2, TRUE)
		if(H.age == AGE_OLD)
			H.adjust_skillrank(/datum/skill/craft/blacksmithing, 1, TRUE)
			H.adjust_skillrank(/datum/skill/craft/armorsmithing, 1, TRUE)
			H.adjust_skillrank(/datum/skill/craft/weaponsmithing, 1, TRUE)
			H.adjust_skillrank(/datum/skill/craft/smelting, 1, TRUE)
			H.adjust_skillrank(/datum/skill/misc/sewing, 1, TRUE) // Worse than the real tailor, so can't steal their job right away
			H.adjust_skillrank(/datum/skill/craft/tanning, 1, TRUE)
		armor = /obj/item/clothing/suit/roguetown/armor/leather/jacket/artijacket
		pants = /obj/item/clothing/under/roguetown/trou/artipants
		shoes = /obj/item/clothing/shoes/roguetown/boots/nobleboot
		shirt = /obj/item/clothing/suit/roguetown/shirt/undershirt/artificer
		backl = /obj/item/storage/backpack/rogue/backpack
		backpack_contents = list(
			/obj/item/rogueweapon/hammer/iron = 1,
			/obj/item/rogueweapon/tongs = 1,
			/obj/item/recipe_book/blacksmithing = 1,
			/obj/item/clothing/mask/rogue/spectacles/golden = 1,
			/obj/item/contraption/linker/master = 1,
			)
		belt = /obj/item/storage/belt/rogue/leather
		beltl = /obj/item/storage/belt/rogue/pouch/coins/rich
		beltr = /obj/item/storage/keyring/guildmaster

/mob/living/carbon/human/proc/guild_announcement()
	set name = "Announcement"
	set category = "GUILDMASTER"
	if(stat)
		return
	var/announcementinput = input("Bellow to the vale", "Make an Announcement") as text|null
	if(announcementinput)
		if(!src.can_speak_vocal())
			to_chat(src,span_warning("I can't speak!"))
			return FALSE
		if (!COOLDOWN_FINISHED(src, guildmaster_announcement))
			to_chat(src, span_warning("You must wait before speaking again."))
			return FALSE
		visible_message(span_warning("[src] takes a deep breath, preparing to make an announcement.."))
		if(do_after(src, 15 SECONDS, target = src)) // Reduced to 15 seconds from 30 on the original Herald PR. 15 is well enough time for sm1 to shove you.
			say(announcementinput)
			priority_announce("[announcementinput]", "The Guildmaster Heralds", 'sound/misc/bell.ogg', sender = src)
			COOLDOWN_START(src, guildmaster_announcement, GUILDMASTER_ANNOUNCEMENT_COOLDOWN)
		else
			to_chat(src, span_warning("Your announcement was interrupted!"))
			return FALSE
